import java.util.regex.Matcher
import java.util.regex.Pattern

ext.enableScreenshotsInAndroidTests = gradle.startParameter.taskNames.size() > 0 &&
        gradle.startParameter.taskNames.get(0) == "connectedDebugAndroidTest"

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:$versions.kotlin"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-allopen"
apply plugin: 'kotlin-kapt'

def appId = "com.patrykkosieradzki.theanimalapp"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId appId
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunner "com.patrykkosieradzki.theanimalapp.utils.ScreenshotsRunner"
        if (project.ext.enableScreenshotsInAndroidTests == true) {
            testInstrumentationRunnerArguments enableScreenshotsInAndroidTests: 'true'
        }
    }

    testOptions {
        if (project.ext.enableScreenshotsInAndroidTests == false) {
            if (!isIdeBuild) {
                execution 'ANDROIDX_TEST_ORCHESTRATOR'
            }
        }
        animationsDisabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "animals"
    productFlavors {
        cats {
            dimension "animals"
//            applicationIdSuffix ".cats"
            buildConfigField "String", "API_URL", "\"https://api.thecatapi.com/v1/\""
        }
        dogs {
            dimension "animals"
//            applicationIdSuffix ".dogs"
            buildConfigField "String", "API_URL", "\"https://api.thedogapi.com/v1/\""
        }
    }

    adbOptions {
        installOptions '-g', '-r'
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        cats {
            res {
                srcDirs 'src\\cats\\res'
            }
        }
        dogs {
            res {
                srcDirs 'src\\dogs\\res'
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":network"))
    implementation(project(":utils"))

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.livedata_ktx
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.viewmodel_ktx

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    implementation "androidx.fragment:fragment-ktx:1.3.0"


    implementation "androidx.navigation:navigation-runtime-ktx:2.3.3"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.3"

    implementation deps.koin.koin_android
    implementation deps.koin.koin_viewmodel

    // Testing
    testImplementation "com.google.truth:truth:1.1.2"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"


    // AndroidTests
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: 'org.mockito'
    }
    androidTestImplementation "com.google.truth:truth:1.1.2"
    androidTestImplementation(deps.koin_test) {
        exclude group: 'org.mockito'
    }
    androidTestImplementation 'org.mockito:mockito-android:3.8.0'
    androidTestImplementation deps.espresso.values()
    androidTestUtil 'androidx.test:orchestrator:1.3.0'
    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'


    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    implementation("org.jetbrains.kotlin:kotlin-stdlib")

    implementation 'com.airbnb.android:lottie:3.6.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
}

allOpen {
    annotation("com.patrykkosieradzki.theanimalapp.utils.AllOpen")
}

def getScreenshotsTestFlavor() {
    if (gradle.startParameter.taskNames.size() > 0) {
        switch (gradle.startParameter.taskNames.get(0)) {
            case "connectedCatsDebugAndroidTest": return "cats"
            case "connectedDogsDebugAndroidTest": return "dogs"
            default: return "UNKNOWN_FLAVOR"
        }
    }

    return "UNKNOWN_FLAVOR"
}

def projectScreenshotsDirectory = "$projectDir/screenshots"
def deviceScreenshotsDirectory = '/sdcard/Pictures/' + appId + "/debug/screenshots/$screenshotsTestFlavor"

def clearScreenshotsTask = task('clearScreenshots', type: Exec) {
    executable "${android.getAdbExe().toString()}"
    args 'shell', 'rm', '-r', deviceScreenshotsDirectory
}

def createScreenshotDirectoryTask = task('createScreenshotDirectory', type: Exec, group: 'reporting') {
    executable "${android.getAdbExe().toString()}"
    args 'shell', 'mkdir', '-p', deviceScreenshotsDirectory
}

def fetchScreenshotsTask = task('fetchScreenshots', type: Exec, group: 'reporting') {
    executable "${android.getAdbExe().toString()}"
    args 'pull', deviceScreenshotsDirectory + '/.', projectScreenshotsDirectory
    finalizedBy {
        clearScreenshotsTask
    }

    dependsOn {
        createScreenshotDirectoryTask
    }

    doFirst {
        new File(projectScreenshotsDirectory).mkdirs()
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'connectedDebugAndroidTest') {
        task.finalizedBy {
            fetchScreenshotsTask
        }
    }
}